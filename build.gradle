buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'

version = "1.7.10-1.0.1.10"
group= "com.azanor.baubles" 
archivesBaseName = "Baubles"

minecraft {
    version = "1.7.10-10.13.2.1236"
    assetDir = "eclipse/assets"
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    
}

// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Create API library zip
task apiJar(type: Jar) {
    from(sourceSets.main.java) {
        include "baubles/api/**"
    }
    classifier = 'api'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            
            artifact apiJar
        }		
    }
    repositories {
        maven {
            url "file://usr/share/nginx/html/maven"
        }
    }
}

artifacts {
    archives deobfJar
    archives apiJar
    archives sourcesJar
}
